<?xml version="1.0" encoding="utf-8"?>
<!--

	ADOBE SYSTEMS INCORPORATED
	Copyright 2009 Adobe Systems Incorporated
	All Rights Reserved.
	
	NOTICE:  Adobe permits you to use, modify, and distribute this file 
	in accordance with the terms of the Adobe license agreement 
	accompanying it.  If you have received this file from a source 
	other than Adobe, then your use, modification, or distribution 
	of it requires the prior written permission of Adobe.

-->

<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" backgroundColor="0xffffff" resize="handleResize()">
	
	<mx:Metadata>
		[Event(name="focusIn",type="flash.events.FocusEvent")]
		[Event(name="focusOut",type="flash.events.FocusEvent")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
		import flashx.textLayout.compose.StandardFlowComposer;
		import flashx.textLayout.container.ContainerController;
		import flashx.textLayout.elements.TextFlow;
		import flashx.textLayout.events.CompositionCompleteEvent;
		import flashx.textLayout.events.TextLayoutEvent;
		
		import mx.events.ScrollEvent;
		
		import textEditBar.SpriteWithIME;
		
		private var _activeFlow:TextFlow;
		
		public function changeContainerSetup(newActiveFlow:TextFlow):void
 		{
 			_activeFlow = newActiveFlow;
 			if (_activeFlow)
 			{
 				if (!_activeFlow.flowComposer)
 					_activeFlow.flowComposer = new StandardFlowComposer();
 				if (_activeFlow.flowComposer.numControllers < 1)
 					_activeFlow.flowComposer.addController(new ContainerController(new SpriteWithIME()));
				rawChildren.addChild(_activeFlow.flowComposer.getControllerAt(0).container);
				_activeFlow.flowComposer.getControllerAt(0).container.addEventListener(FocusEvent.FOCUS_IN,handleFocusEvent);
				_activeFlow.flowComposer.getControllerAt(0).container.addEventListener(FocusEvent.FOCUS_OUT,handleFocusEvent);
				_activeFlow.addEventListener(CompositionCompleteEvent.COMPOSITION_COMPLETE, setupScrollBar);
				handleResize();
 			}
  		}
 		private function handleFocusEvent(event:FocusEvent):void {
			this.dispatchEvent(event);
		}
		public function handleResize():void
		{
			if (!_activeFlow)
				return;
				
			var newFrameWidth:Number = width-25;
			var newFrameHeight:Number = height;
			if(_activeFlow.flowComposer) {
				var cont:ContainerController = _activeFlow.flowComposer.getControllerAt(0);
				if (cont.container)
					updateFrameDimensions(cont,0,0,newFrameWidth,newFrameHeight);

				_activeFlow.flowComposer.updateAllControllers();
				
				if (_activeFlow.interactionManager && _activeFlow.interactionManager.hasSelection())
					_activeFlow.flowComposer.getControllerAt(0).scrollToRange(_activeFlow.interactionManager.activePosition,_activeFlow.interactionManager.anchorPosition);	
			}
		}
				
 		/** helper function to update a frame's dimensions */
 		private function updateFrameDimensions(controller:ContainerController,x:Number,y:Number,w:Number,h:Number):void
 		{
 			var tc:DisplayObject = controller.container;
 			
   			if (tc.x != x)
 				tc.x = x;
 			if (tc.y != y)
 				tc.y = y;
			controller.setCompositionSize(w,h);
 		}
			private function setupScrollBar(event:CompositionCompleteEvent):void {
				var textHeight:int = Math.ceil(_activeFlow.flowComposer.getControllerAt(0).getContentBounds().height);
				var oldPos:Number = _activeFlow.flowComposer.getControllerAt(0).verticalScrollPosition;
				
				if (textHeight < _activeFlow.flowComposer.getControllerAt(0).compositionHeight) {
					scroll.enabled = scroll.visible = false;
				} else {
					scroll.enabled = scroll.visible =  true;
					scroll.minScrollPosition = 0;
					scroll.maxScrollPosition = textHeight - _activeFlow.flowComposer.getControllerAt(0).compositionHeight;
					scroll.lineScrollSize = 20;
					scroll.pageSize = _activeFlow.flowComposer.getControllerAt(0).compositionHeight;
				}
				_activeFlow.flowComposer.getControllerAt(0).verticalScrollPosition = oldPos;
			}
			private function scrollListener(event:mx.events.ScrollEvent):void {
				_activeFlow.removeEventListener(ScrollEvent.SCROLL, scrollTextFlow);
				_activeFlow.flowComposer.getControllerAt(0).verticalScrollPosition = event.position;
				_activeFlow.addEventListener(ScrollEvent.SCROLL, scrollTextFlow);
			}
			
			private function scrollTextFlow(event:TextLayoutEvent):void {
				scroll.removeEventListener(mx.events.ScrollEvent.SCROLL, scrollListener);
				scroll.scrollPosition = Math.ceil( _activeFlow.flowComposer.getControllerAt(0).verticalScrollPosition);
				scroll.addEventListener(TextLayoutEvent.SCROLL, scrollListener);
			}		
		]]>
	</mx:Script>
	<mx:VScrollBar id="scroll" height="100%" scroll="scrollListener(event)" right="0"/>
</mx:Canvas>
